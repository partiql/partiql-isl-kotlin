// Documentation for using Gradle with Kotlin is here: https://kotlinlang.org/docs/reference/using-gradle.html

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.dokka' version '0.9.18'
    id 'jacoco'
}

group = "org.partiql"
version = "0.1.0-SNAPSHOT"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()

    // jcenter required for dokka
    jcenter()
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


dependencies {
    api 'com.amazon.ion:ion-java:1.8.0'
    api 'com.amazon.ion:ion-element:0.2.0'
    api 'com.amazon.ion:ion-schema-kotlin:1.2.1'
    api 'org.partiql:partiql-ir-generator-runtime:0.4.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.0'
    testImplementation 'org.assertj:assertj-core:3.12.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
}

test {
    useJUnitPlatform()
    jacoco {
        destinationFile = file("${buildDir}/jacoco/test.exec")
    }
}

dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    from "src"
    archiveClassifier.set("sources")
}

task javadocJar(type: Jar) {
    from dokka
    archiveClassifier.set("javadoc")
}

java {
    sourceSets {
        main.java.srcDirs = ["src"]
        main.resources.srcDirs = ["resources"]
        test.java.srcDirs = ["test"]
        test.resources.srcDirs = ["test-resources"]
    }
}


jacocoTestReport {
    // Adjust the output of the test report
    reports {
        xml.enabled true
        csv.enabled false
    }
}

tasks.jacocoTestReport {
    dependsOn(tasks.test) // tests are required to run before generating the report
}


plugins.withId('org.jetbrains.kotlin.jvm', { _ ->
    sourceSets {
        main.kotlin.srcDirs = ["src"]
        test.kotlin.srcDirs = ["test"]
    }
})

import org.partiql.pig.cmdline.Command
import org.partiql.pig.cmdline.TargetLanguage
import org.partiql.pig.errors.PigException

task generatePigDomains {
    group = "code generation"

    // TODO: in the future, utilize the API created as part of this ticket:
    // https://github.com/partiql/partiql-ir-generator/issues/5

    def typeUniverse = new File(projectDir, "src/org/partiql/ionschema/model/isl.ion")
    def outputFile = new File(projectDir, "src/org/partiql/ionschema/model/isl-model.kt")
    def targetLanguage = new TargetLanguage.Kotlin("org.partiql.ionschema.model")
    def cmd = new Command.Generate(typeUniverse, outputFile, targetLanguage)

    // TODO:
    // Tell gradle about the input and output of this task so that it only invokes pig when
    // the input file has changed or the output file is missing.

    doLast {
        try {
            org.partiql.pig.MainKt.generateCode(cmd)
        } catch(PigException e) {
            System.err.println("Failed to generate PIG domains: ${e.error}")
            throw new TaskExecutionException(generatePigDomains, e)
        }
    }
}

compileKotlin.dependsOn generatePigDomains

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "partiql-isl-kotlin"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "PartiQL ISL Kotlin"
                packaging = "jar"
                url = "https://github.com/partiql/partiql-isl-kotlin"
                description = "A Kotlin implementation of an Ion Schema Language (ISL) in-memory object model"
                scm {
                    connection = "scm:git@github.com:partiql/partiql-isl-kotlin.git"
                    developerConnection = "scm:git@github.com:partiql/partiql-isl-kotlin.git"
                    url = "git@github.com:partiql/partiql-isl-kotlin.git"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "Amazon Ion Team"
                        email = "ion-team@amazon.com"
                        organization = "PartiQL"
                        organizationUrl = "https://github.com/partiql"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://aws.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    // Allow publishing to maven local even if we don't have the signing keys
    // This works because when not required, the signing task will be skipped
    // if signing.keyId, signing.password, signing.secretKeyRingFile, etc are
    // not present in gradle.properties.
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.maven
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
